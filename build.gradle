plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.7' apply false
    id 'io.spring.dependency-management' version '1.1.7'
    id 'com.diffplug.spotless' version '6.25.0'
}

group = 'com.fastline'
version = '0.0.1-SNAPSHOT'
description = 'fastline'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'com.diffplug.spotless'

    repositories {
        mavenCentral()
    }

    dependencyManagement {
        imports {
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:2025.0.0"
        }
    }

    dependencies {
        compileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'
        implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
        implementation 'org.springframework.boot:spring-boot-starter-web'
        testImplementation 'org.springframework.boot:spring-boot-starter-test'
    }

    spotless {
        java {
            googleJavaFormat("1.17.0")
            indentWithTabs(2)
            endWithNewline()
            removeUnusedImports()
            trimTrailingWhitespace()
            target("src/**/*.java")
        }

        format("misc") {
            target("**/*.gradle.kts", "**/*.md", "**/.gitignore")
            targetExclude(".release/*.*")
            indentWithSpaces()
            trimTrailingWhitespace()
            endWithNewline()
        }
    }

    tasks.named('test') {
        useJUnitPlatform()
    }
}

project(':common') {
    bootJar.enabled = false
    jar.enabled = true
}

project(':delivery-service') {
    dependencies {
        implementation project(':common')
    }
}
project(':hub-service') {
    dependencies {
        implementation project(':common')
    }
}

project(':ai-service') {
    dependencies {
        implementation project(':common')
    }
}


project(':vendor-service') {
    dependencies {
        implementation project(':common')
    }
}